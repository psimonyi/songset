songsheet = _{ soi ~ head ~ nl ~ nl+ ~ body ~ nl* ~ eoi }

head = { lws* ~ sexp ~ (lws* ~ nl? ~ lws* ~ sexp)* }

body = { block ~ (nl ~ nl ~ block)* }

block = { line ~ (nl ~ line)* }

line = { indent? ~ (text | sexp)+ }

indent = { lws+ }

text = { ( !(nl | parens) ~ any )+ }
parens = _{ lparen1 | lparen2 | rparen1 | rparen2 }

sexp = { sexp1 | sexp2 }

sexp1 = _{ lparen1 ~ sexp_inner ~ rparen1 }
sexp2 = _{ lparen2 ~ sexp_inner ~ rparen2 }
sexp_inner = _{ keyword ~ (lws+ ~ sexp_body)? }

keyword = { ( !(parens | lws | nl) ~ any )+ }

sexp_body = _{ (text | sexp)+ }

lparen1 = _{ "[[" }
rparen1 = _{ "]]" }
lparen2 = _{ "⟦" } // U+27E6 MATHEMATICAL LEFT WHITE SQUARE BRACKET
rparen2 = _{ "⟧" } // U+27E7 MATHEMATICAL RIGHT WHITE SQUARE BRACKET
lws = _{ " " }
nl = _{ "\n" }
